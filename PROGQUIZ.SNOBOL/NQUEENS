//SNOBOL   JOB 1,1,MSGCLASS=A,REGION=512K
//DATA   EXEC  PGM=SNOBOL4                                              00000200
//STEPLIB  DD  DSN=TMP.SNOBOL4.LINKLIB,DISP=SHR
//FT06F001 DD  SYSOUT=A                                                 00000400
//FT07F001 DD  SYSOUT=A                                                 00000500
//FT05F001 DD  *
//
        * N QUEENS PROBLEM
        * SET N TO THE DESIRED NUMBER.  THE PROGRAM PRINTS OUT ALL SOLUTION BOARDS.
            N = 5
            NM1 = N - 1; NP1 = N + 1; NSZ = N * NP1; &STLIMIT = 10 ** 9; &ANCHOR = 1
            DEFINE('SOLVE(B)I')
        * THIS PATTERN TESTS IF THE FIRST QUEEN ATTACKS ANY OF THE OTHERS:
            TEST = BREAK('Q') 'Q' (ARBNO(LEN(N) '-') LEN(N) 'Q'
        +	      | ARBNO(LEN(NP1) '-') LEN(NP1) 'Q'
        +	      | ARBNO(LEN(NM1) '-') LEN(NM1) 'Q')
            P = LEN(NM1) . X LEN(1); L = 'Q' DUPL('-',NM1) ' '
            SOLVE()        :(END)
        SOLVE	EQ(SIZE(B),NSZ) 	    :S(PRINT)
        * ADD ANOTHER ROW WITH A QUEEN:
            B = L B
        LOOP	I = LT(I,N) I + 1 :F(RETURN)
            B TEST :S(NEXT)
            SOLVE(B)
        * TRY QUEEN IN NEXT SQUARE:
        NEXT	B P = '-' X :(LOOP)
        PRINT	SOLUTION = SOLUTION + 1
            OUTPUT = 'SOLUTION NUMBER ' SOLUTION ' IS:'
        PRTLOOP B LEN(NP1) . OUTPUT = :S(PRTLOOP)F(RETURN)
        END

